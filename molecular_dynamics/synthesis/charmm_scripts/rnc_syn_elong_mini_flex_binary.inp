* CHARMM
* Edward P. O'Brien Jr. UMD/NIH
* Edited by Dan Nissley 03/21/18 
!======== BEGIN: Set parameters ===========

prnlev 3
bomlev 0

set total @total

  ! Seed the random number generator
  rand unif isee @rand

  ! read parameter and topology files
  read rtf card name @top
  read param card flex name @param

  ! Read psf and coor files from previous nascent chain length
  read psf file name @psf
  read coor file name @cor

  ! Set friction forces
  scalar fbeta set @fbsolu sele segid A .or. segid B end
  SCAL FBETA SHOW sele segid A .or. segid B end

  ! speeds up the non-bonded list generation
  nbond bycc
  update DEBY SCRE 10.0
  eten on

  ! delete the ribosome coordinates, because the 'join' command doesn't work otherwise
  dele atoms sele .not. segid A end

  ! Step 1 - add a new residue through the patching command
  calc connect @total-1

  ! at this point we have a PSF for a molecule of some size and
  ! now we want to extend it
  read sequence G@total 1

  gene new setup

  patch link new 1 A @connect ! to join this residue onto previous system
    
  autogen angl dihe

  join A new renumber! to get new residue into same segment
  ! Step 2 - now define new coordinates for the C-terminal residue

  scalar x set 2.0 sele segid A .and. type A .and. resid @total end
  scalar y set 0.0 sele segid A .and. type A .and. resid @total end
  scalar z set 0.0 sele segid A .and. type A .and. resid @total end
    
  !! READ THE L22 COORDINATES SAVED FROM AFTER PREVIOUS DYNAMICS RUN BACK IN
  read psf append card name @outdir@traj_l22_struc.psf
  read coor append card name @outdir@traj_l22_struc.cor

  !! READ THE RIBO ONLY COORDINATES BACK IN; THESE ARE THE CRYSTALLOGRAPHIC COORDINATES
  read psf append card name @ribopsf
  read coor append card name @ribostruc

  ! turn off ALL interaction between RIBO sites
  ! retain RIBO-A, RIBO-B, A-B, A-A, and B-B interactions
  block 3
  call 1 sele segid RIBO end
  call 2 sele segid A end
  call 3 sele segid B end
  coeff 1 1 0.0
  coeff 2 2 1.0
  coeff 3 3 1.0
  coeff 1 2 1.0
  coeff 1 3 1.0
  coeff 2 3 1.0
  end

  ! place planar restraint so the nascent chain doesn't go behind the PTC
  MMFP
  GEO plane xref 1.0 yref 0.0 zref 0.0 xdir 1.0 -
  exponential force 50 droff 2.0 p1 0.05 -
  sele segid A end
  END

  ! place planar restraint so nascent chain can't touch bottom of ribosome surface
  calc tval @total - 49 ! ignore first 50 residues for this restraint
  MMFP
  GEO plane xref 58.0 yref 0.0 zref 0.0 xdir 1.0 -
  exponential force 50 droff 2.0 p1 0.05 -
  sele segid A .and. (.not. resid @tval:@total) .and. (prop x .gt. 60) end ! ignore first 50 residues for this restraint
  END

  ! place planar restraints so nascent chain is directed into tunnel when it is short
  if @total .le. 15 then

   !add a series of five planar restraints to direct the nascent chain
   MMFP              
   GEO plane xref 0.0 yref 20.0 zref 0.0 ydir 1.0 -
   exponential force 50 droff 2.0 p1 0.05 -
   sele segid A end
   END

   MMFP
   GEO plane xref 0.0 yref -10.0 zref 0.0 ydir -1.0 -
   exponential force 50 droff 2.0 p1 0.05 -           
   sele segid A end
   END

   MMFP
   GEO plane xref 0.0 yref 0.0 zref 20.0 zdir 1.0 -
   exponential force 50 droff 2.0 p1 0.05 -
   sele segid A end    
   END

   MMFP
   GEO plane xref 0.0 yref 0.0 zref -12.0 zdir -1.0 -
   exponential force 50 droff 2.0 p1 0.05 -
   sele segid A end                 
   END

  endif

  ! minimize steric clashes of the newly inserted residue
  cons fix sele .not.(segid A .and. resid @total) end

  mini abnr nstep 2000 nprint 1000

  ! turn off the cons fix constraints
  cons fix sele none end

  ! calc temp variable
  calc junk @total-15

  ! apply restraint to C-terminal bead
  MMFP
  GEO sphere harm -
  xref 6.0 yref 0 zref 0 -
  force 50 droff 0.0 select resid @total .and. segid A .and. type A end
  END

  ! select interaction sites around the exit tunnel
  define s1 sele segid RIBO .and. (prop x .gt. 3 .and. point 6 0 0 CUT 12) end

  ! select all interaction sites not in the box around the exit tunnel
  define s2 sele segid RIBO .and. (.not. (s1)) end

  ! select residues in the nascent chain that are not near the PTC
  define s3 sele segid A .and. (.not. resid @junk:@total) end

  ! select all of segid B and hold it fixed
  define s4 sele segid B end

  ! cons harm force 0.5 sele (s1) end

  ! fix everything other than the C-terminal 15 residues of the nascent chain 
  cons fix sele (s1) .or. (s2) .or. (s3) .or. (s4) end

  mini sd nstep 2000 nprint 1000
  mini abnr nstep 2000 nprint 1000

  ! write the initial structure to use for the next nascent chain length
  open write unit 10 file name @outdir@traj_r@total_initstruc_@prot.psf
  write psf file name unit 10
  close unit 10

  ! write crd file
  open write unit 10 file name @outdir@traj_r@total_initstruc_@prot.cor
  write coor file unit 10
  close unit 10

  close unit 21

STOP
